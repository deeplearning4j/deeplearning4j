/* ******************************************************************************
 *
 *
 * This program and the accompanying materials are made available under the
 * terms of the Apache License, Version 2.0 which is available at
 * https://www.apache.org/licenses/LICENSE-2.0.
 *
 *  See the NOTICE file distributed with this work for additional
 *  information regarding copyright ownership.
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations
 * under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 ******************************************************************************/

//
//  @author raver119@gmail.com
//
#include <execution/LaunchContext.h>
#include <exceptions/cuda_exception.h>
#include <system/op_boilerplate.h>
#include <loops/reduce_float.h>
#include <loops/scalar.h>
#include <loops/legacy_ops.h>
#include <helpers/DebugHelper.h>
#include <types/types.h>
#include <ops/specials_cuda.h>
#include <cuda.h>
#include <cuda_runtime.h>

using namespace simdOps;

////////////////////////////////////////////////////////////////////////
template <typename X, typename Z, typename OpType>
SD_KERNEL void simpleReduce(const void *x, const sd::LongType *outerXTadShapeInfo, const sd::LongType *innerXTadShapeInfo,
                            void *extraParams, void *vreductionBuffer, void *z, const sd::LongType *zShapeInfo) {

    functions::reduce::ReduceFloatFunction<X,Z>::template transformCudaXD<OpType>(x, outerXTadShapeInfo, innerXTadShapeInfo, extraParams, vreductionBuffer, z, zShapeInfo);
}

////////////////////////////////////////////////////////////////////////
template <typename X, typename Z, typename OpType>
SD_KERNEL void simpleScalar(const void *x, const sd::LongType *xShapeInfo,
                            void *extraParams,
                            void *z, const sd::LongType *zShapeInfo, long long int *dimension, long long int dimensionLength,
                            void *reductionBuffer,
                             const sd::LongType *tadOnlyShapeInfo) {

    functions::reduce::ReduceFloatFunction<X, Z>::template execScalarCuda<OpType>(x, xShapeInfo, extraParams, z, zShapeInfo, reductionBuffer, tadOnlyShapeInfo);
}

namespace functions {
namespace reduce    {

////////////////////////////////////////////////////////////////////////
template <typename X, typename Z>
template <typename OpType>
SD_DEVICE void ReduceFloatFunction<X,Z>::aggregatePartials(void *vsPartials, sd::LongType tid, sd::LongType numItems, void *vextraParams) {

    // start the shared memory loop on the next power of 2 less
    // than the block size.  If block size is not a power of 2,
    // accumulate the intermediate sums in the remainder range.
    using Y = typename OpType::InterType;
    auto sPartials = reinterpret_cast<Y*>(vsPartials);
    auto extraParams = reinterpret_cast<Z*>(vextraParams);

    sd::LongType floorPow2 = numItems;

    if (floorPow2 & (floorPow2 - 1)) {

        while (floorPow2 & (floorPow2 - 1))
            floorPow2 &= floorPow2 - 1;

        if (tid >= floorPow2)
            sPartials[tid - floorPow2] = OpType::update(sPartials[tid - floorPow2], sPartials[tid], extraParams);

        __syncthreads();
    }

    for (sd::LongType activeThreads = floorPow2 >> 1; activeThreads; activeThreads >>= 1) {
          if (tid < activeThreads && tid + activeThreads < numItems)
            sPartials[tid] = OpType::update(sPartials[tid], sPartials[tid + activeThreads], extraParams);

        __syncthreads();
    }
}

////////////////////////////////////////////////////////////////////////
template <typename X, typename Z>
template <typename OpType>
SD_DEVICE void ReduceFloatFunction<X,Z>::transformCudaXD(const void *vx, const sd::LongType *outerXTadShapeInfo, const sd::LongType *innerXTadShapeInfo,
                                                          void *vextraParams, void *vreductionBuffer,
                                                          void *vz, const sd::LongType *zShapeInfo) {

  auto x = reinterpret_cast<const X*>(vx);
  auto z = reinterpret_cast<Z*>(vz);
  auto extraParams = reinterpret_cast<Z*>(vextraParams);

  //shared memory space for storing intermediate results
  __shared__ Z sPartials[SD_CUDA_BLOCK_SIZE];
  __shared__  sd::LongType tadLen, numTads;
  __shared__ bool sameOffsets;

  if (threadIdx.x == 0) {
    sameOffsets = shape::haveSameShapeAndStrides(zShapeInfo, outerXTadShapeInfo);

    tadLen  = shape::length(innerXTadShapeInfo);
    numTads = shape::length(outerXTadShapeInfo);
  }
  __syncthreads();

  sd::LongType coords[SD_MAX_RANK];

  for (sd::LongType r = blockIdx.x; r < numTads; r += gridDim.x) {

    INDEX2COORDS(r, shape::rank(outerXTadShapeInfo), shape::shapeOf(outerXTadShapeInfo), coords);
    sd::LongType outerOffset,zOffset;
    COORDS2INDEX(shape::rank(outerXTadShapeInfo), shape::stride(outerXTadShapeInfo), coords, outerOffset);
    if(sameOffsets) {
      sameOffsets = outerOffset;
    } else {
      COORDS2INDEX(shape::rank(zShapeInfo), shape::stride(zShapeInfo), coords, zOffset);
    }
    const X* xTad = x + outerOffset;
    sPartials[threadIdx.x] = OpType::startingValue(xTad);

    for (int i = threadIdx.x; i < tadLen; i += blockDim.x) {
      sd::LongType innerOffset;
      COORDS2INDEX(shape::rank(innerXTadShapeInfo), shape::stride(innerXTadShapeInfo), coords, innerOffset);
      sPartials[threadIdx.x] = OpType::update(sPartials[threadIdx.x], OpType::op(xTad[innerOffset], extraParams), extraParams);
    }
    __syncthreads();

    // aggregate. do NOT reduce for elements > tadLen
    aggregatePartials<OpType>(sPartials, threadIdx.x, sd::math::sd_min<int>(blockDim.x, tadLen), extraParams);
    __syncthreads();

    if (threadIdx.x == 0)
      z[zOffset] = OpType::postProcess(sPartials[threadIdx.x], tadLen, extraParams);
  }
}
////////////////////////////////////////////////////////////////////////
template <typename X, typename Z>
template <typename OpType>
SD_DEVICE void ReduceFloatFunction<X, Z>::execScalarCuda(const void *vx, const sd::LongType *xShapeInfo,
                                                         void *vextraParams, void *vz, const sd::LongType *zShapeInfo,
                                                         void *vreductionBuffer, const sd::LongType *tadOnlyShapeInfo) {
  auto x = reinterpret_cast<const X *>(vx);
  auto z = reinterpret_cast<Z *>(vz);
  auto extraParams = reinterpret_cast<Z *>(vextraParams);
  auto reductionBuffer = reinterpret_cast<Z *>(vreductionBuffer);

  auto tid = blockDim.x * blockIdx.x + threadIdx.x;
  using Y = typename OpType::InterType;
  // shared memory space for storing intermediate results
  __shared__ Y sPartials[SD_CUDA_BLOCK_SIZE];
  __shared__ sd::LongType len;

  if (threadIdx.x == 0) {
    len = shape::length(xShapeInfo);
  }
  __syncthreads();

  sPartials[threadIdx.x] = OpType::startingValue(x);

  for (int i = tid; i < len; i += blockDim.x * gridDim.x) {
    sd::LongType xCoords[SD_MAX_RANK];
    sd::LongType xOffset;
    INDEX2COORDS(i, shape::rank(xShapeInfo), shape::shapeOf(xShapeInfo), xCoords);
    COORDS2INDEX(shape::rank(xShapeInfo), shape::stride(xShapeInfo), xCoords, xOffset);
    sPartials[threadIdx.x] = OpType::update(sPartials[threadIdx.x], OpType::op(x[xOffset], extraParams), extraParams);
  }

  __syncthreads();
  aggregatePartials<OpType>(sPartials, threadIdx.x, sd::math::sd_min<int>(blockDim.x, len), extraParams);
  __syncthreads();

  if (gridDim.x > 1) {
    unsigned int *tc = (unsigned int *)reductionBuffer;
    __shared__ bool amLast;

    tid = threadIdx.x;
    if (threadIdx.x == 0)
      reductionBuffer[blockIdx.x] = sPartials[0];  // this->postProcess(sPartials[0],len,extraParams);

    __threadfence();
    __syncthreads();

    if (threadIdx.x == 0) {
      unsigned int ticket = atomicInc(&tc[16384], gridDim.x);
      amLast = (ticket == gridDim.x - 1);
    }

    __syncthreads();

    if (amLast) {
      tc[16384] = 0;
      sPartials[threadIdx.x] = OpType::startingValue(x);

      for (int i = threadIdx.x; i < gridDim.x; i += blockDim.x)
        sPartials[threadIdx.x] = OpType::update(sPartials[threadIdx.x], reductionBuffer[i], extraParams);

      __syncthreads();
      aggregatePartials<OpType>(sPartials, threadIdx.x, sd::math::sd_min<int>(gridDim.x, blockDim.x), extraParams);
      __syncthreads();

      if (threadIdx.x == 0) {
        z[0] = OpType::postProcess(sPartials[0], len, extraParams);
      }
    }
  } else {
    if (threadIdx.x == 0) {
      unsigned int *tc = (unsigned *)reductionBuffer;
      tc[16384] = 0;
      z[0] = OpType::postProcess(sPartials[0], len, extraParams);
    }
  }
}

////////////////////////////////////////////////////////////////////////
template <typename X, typename Z>
template<typename OpType>
SD_HOST void ReduceFloatFunction<X,Z>::intermediateXD(dim3 launchDims, cudaStream_t *stream,
                                                       const void *x, const sd::LongType *dXShapeInfo, const sd::LongType *hXShapeInfo,
                                                       void *extraParams, void *vreductionBuffer,
                                                       void *z, const sd::LongType *dZShapeInfo, const sd::LongType *hZShapeInfo,
                                                       const  sd::LongType *dims) {

    if(shape::isEmptyConst(hXShapeInfo)) {
        const auto startingVal = std::is_same<OpType, simdOps::Mean<X,Z>>::value ? sd::DataTypeUtils::nanOrZero<Z>() : static_cast<Z>(OpType::startingValue(reinterpret_cast<const X*>(x)));
        auto res = cudaMemcpyAsync(sd::LaunchContext::defaultContext()->getScalarPointer(), &startingVal, sizeof(Z), cudaMemcpyHostToDevice, *stream);
        if (res != 0)
            throw sd::cuda_exception::build("ReduceFloatFunction<X,Z>::intermediateXD: failed to copy temporary scalar", res);

        auto ptr = sd::LaunchContext::defaultContext()->getScalarPointer();

        // scalar assign
        functions::scalar::ScalarTransform<Z, Z, Z>::executeCudaShaped(launchDims, stream, 14, z, dZShapeInfo, hZShapeInfo, z, dZShapeInfo, hZShapeInfo, ptr, nullptr);
    }
    else {

        const int zRank = shape::rank(hZShapeInfo);
        const int tadRank = shape::rank(hXShapeInfo) - zRank;

        auto outerPack = sd::ConstantShapeHelper::getInstance().createSubArrShapeInfo(hXShapeInfo, dims, zRank);
        auto innerPack = sd::ConstantShapeHelper::getInstance().createSubArrShapeInfo(hXShapeInfo, dims+zRank, tadRank);

        simpleReduce<X, Z, OpType><<<launchDims.x, launchDims.y, launchDims.z, *stream>>>(x,
                                                                                          reinterpret_cast<sd::LongType const*>(outerPack->special()),
                                                                                          reinterpret_cast<sd::LongType const*>(innerPack->special()),
                                                                                          extraParams, vreductionBuffer, z, dZShapeInfo);
    }
}

////////////////////////////////////////////////////////////////////////
template <typename X, typename Z>
template<typename OpType>
SD_HOST void ReduceFloatFunction<X,Z>::intermediateScalar(dim3 launchDims, cudaStream_t *stream,
                                                           const void *x, const sd::LongType *xShapeInfo, const sd::LongType *hXShapeInfo,
                                                           void *extraParams,
                                                           void *z, const sd::LongType *dZShapeInfo, const sd::LongType *hZShapeInfo,  sd::LongType *dimension,
                                                           sd::LongType dimensionLength,
                                                           void *reductionBuffer,
                                                           const sd::LongType *tadOnlyShapeInfo) {

    if (shape::isEmptyConst(hXShapeInfo)) {
        const auto startingVal = std::is_same<OpType, simdOps::Mean<X,Z>>::value ? sd::DataTypeUtils::nanOrZero<Z>() : static_cast<Z>(OpType::startingValue(reinterpret_cast<const X*>(x)));

        auto res = cudaMemcpyAsync(z, &startingVal, sizeof(Z), cudaMemcpyHostToDevice, *stream);
        if (res != 0)
            throw sd::cuda_exception::build("ReduceFloatFunction<X,Z>::intermediateScalar: failed to copy resulting scalar", res);
    }
    else {
        simpleScalar<X, Z, OpType> <<<launchDims.x, launchDims.y, launchDims.z, *stream>>>(x, xShapeInfo, extraParams, z, dZShapeInfo, dimension, dimensionLength, reductionBuffer, tadOnlyShapeInfo);
    }


    sd::DebugHelper::checkErrorCode(stream, "ReduceFloatFunction intermediateScalar(...) failed");

}

////////////////////////////////////////////////////////////////////////
template <typename X, typename Y>
SD_HOST void ReduceFloatFunction<X,Y>::execReduceScalar(dim3 launchDims, cudaStream_t *stream,
                                                        const int opNum,
                                                        const void *x, const sd::LongType *xShapeInfo, const sd::LongType *hXShapeInfo,
                                                        void *extraParams,
                                                        void *z, const sd::LongType *dZShapeInfo, const sd::LongType *hZShapeInfo, long long int *dimension,
                                                         long long int dimensionLength,
                                                        void *reductionBuffer,
                                                        const sd::LongType *tadOnlyShapeInfo) {

        DISPATCH_BY_OPNUM_TT(intermediateScalar, PARAMS(launchDims, stream, x, xShapeInfo, hXShapeInfo, extraParams, z, dZShapeInfo, hZShapeInfo, dimension, dimensionLength, reductionBuffer, tadOnlyShapeInfo), OPS_A(REDUCE_FLOAT_OPS));
        sd::DebugHelper::checkErrorCode(stream, "execReduceScalarFloat(...) failed");
}

////////////////////////////////////////////////////////////////////////
template <typename X, typename Y>
SD_HOST void ReduceFloatFunction<X,Y>::execReduceXD(dim3 launchDims, cudaStream_t *stream, const int opNum,
                                                    const void *x, const sd::LongType *dXShapeInfo, const sd::LongType *hXShapeInfo,
                                                    void *extraParams, void *vreductionBuffer,
                                                    void *z, const sd::LongType *dZShapeInfo, const sd::LongType *hZShapeInfo, const  sd::LongType *dims) {

    if(shape::length(hZShapeInfo) == 1)  {
        ReduceFloatFunction<X,Y>::execReduceScalar(launchDims, stream, opNum, x, dXShapeInfo, hXShapeInfo, extraParams, z, dZShapeInfo, hZShapeInfo, nullptr, 0, vreductionBuffer, nullptr);
    }
    else {
        DISPATCH_BY_OPNUM_TT(intermediateXD, PARAMS(launchDims, stream, x, dXShapeInfo, hXShapeInfo, extraParams, vreductionBuffer, z, dZShapeInfo, hZShapeInfo, dims), OPS_A(REDUCE_FLOAT_OPS));
    }
    sd::DebugHelper::checkErrorCode(stream, "ReduceFloatFunction execReduceXD(...) failed");
}

////////////////////////////////////////////////////////////////////////

}
}

